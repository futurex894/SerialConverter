; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
;git仓库路径
#define GitPath SourcePath
;软件名称
#define MyAppName "调试工具箱"
;软件版本
#define MyAppVersion "1.0.0"
#define MyAppPublisher "TryAlice"
#define MyAppExeName "SerialConverter.exe"
;安装包输出路径
#define FileOutput GitPath
;安装包图标
#define SetupIconPath GitPath+"SerialConverter\Assets\Terminal.ico"
;需要打包的文件（删除配置文件后打包）
#define ExePath GitPath+"SerialConverter\bin\Release\net8.0\publish"


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{6EE123B4-96ED-9DBD-FCFB-4E2C6FE810CF}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputDir={#FileOutput}
OutputBaseFilename=调试工具箱-Setup-{#MyAppVersion}
SetupIconFile={#SetupIconPath}
Compression=lzma
SolidCompression=yes
WizardStyle=modern
;以管理员身份运行
PrivilegesRequired=admin

[Languages]
Name: "chinese"; MessagesFile: "ChineseSimplified.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "{#ExePath}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#ExePath}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}";AfterInstall: SetElevationBit('{autoprograms}\{#MyAppName}.lnk')
;Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name:"{autodesktop}\{#MyAppName}"; Filename:"{app}\{#MyAppExeName}";AfterInstall: SetElevationBit('{autodesktop}\{#MyAppName}.lnk')
[Code]

procedure SetElevationBit(Filename: string);
var
  Buffer: string;
  Stream: TStream;
begin
  Filename := ExpandConstant(Filename);
  Log('Setting elevation bit for ' + Filename);

  Stream := TFileStream.Create(FileName, fmOpenReadWrite);
  try
    Stream.Seek(21, soFromBeginning);
    SetLength(Buffer, 1);
    Stream.ReadBuffer(Buffer, 1);
    Buffer[1] := Chr(Ord(Buffer[1]) or $20);
    Stream.Seek(-1, soFromCurrent);
    Stream.WriteBuffer(Buffer, 1);
  finally
    Stream.Free;
  end;
end;

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

